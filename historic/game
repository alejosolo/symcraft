WM = 10 //world margin
WMO =l WM WM //margin origin
MMR =l 24 2 128 128 //minimap rect
MaxUnits = 1200

clearGame Info = !Cycle:0 = !WW:0 = !WH:0 = !GameInfo:Info = !Owned: vec 32
 = !Units: vec MaxUnits+300*300 = !Cells: d MaxUnits Units = !ThisPlayer:ø
 = !Vs: vec Units,len = !VsI:0 = !FreeIds: rng 1 1200 
 = !Scheds:[] = !NQs:[] = !Trans:[] = !Orders:[] = !NewUnits:[] = !DelUnits:[]

cellIndex P = WW*P,1 + P,0 + MaxUnits

cell P = aget P,cellIndex Units
setCell P V = aset P,cellIndex V Units
updArea U F = xy @U.tile @U.size
            | e <P:[X Y] = I:Y*WW+X = aset I (aget I Cells|F) Cells = mmUpdate P>

PudTilesets =q summer winter wasteland swamp
PudTeams =u nobody:0 neutral:0 capturable:0 computer:1 person:2 rescueable:2
PudPlayers =q ø ø neutral ø computer person capturable rescueable
Critters =u summer:\sheep wasteland:\boar winter:\seal swamp:\hellhog

loadPud Path = u name:Path,pne,1 file:Path | clearGame
 = TS:\summer = Gfxes:ø = Us:ø = Players:ø = Us:ø
 = setTile:<P I = N: m ?+P Dirs | k (inRect WR ?)
                = Tiles,I|u tile:P disp:32P id:P,cellIndex neibs:N | setCell P>
 = era:<[N @_] = !TS:PudTilesets,N = !Tileset:Tilesets.TS = !Trns:Tileset.trns
               = !Tiles:Tileset.tiles = !Gfxes:Tileset.gfxes>
 = sres:<R Xs = grp 2 Xs | m ul | i | e <[A I]=!Players.I.R:A>>
 = Handlers:u
     “DESC”:<Xs = !GameInfo|u desc:(t {pos 0 Xs;32} Xs | utf8)>
     “ERA ”:era “ERAX”:era
     “DIM ”:<[W:@2.ul H:@2.ul @_] = !WW:W+2WM = !WH:H+2WM = !MMD:128%(WW-2WM)
                                   = !MMWH:l MMD MMD = !WR:[WM WM WW-2WM WH-2WM]>
     “OWNR”:<Xs = m PudPlayers,? Xs | i | e <[P I]
                  = T:UTs.player = !T.color:P,<\neutral=\yellow; _=PlayerColors,I>
                  = U: unit 0 T = {P,n = !U.nobody:√}
                  = !Players.I: U|u name:“Player$I” playable:P,<\person>
                                    rescueable:(q capturable rescueable | any P)
                                    tile:WMO team:PudTeams.P>>
     “SIDE”:<Xs = m (q human orc neutral),? Xs | i | e <[S I]=!Players.I.side:S>>
     “SGLD”:<X=sres \gold X> “SLBR”:<X=sres \wood X> “SOIL”:<X=sres \oil X>
     “AIPL”:<Xs = i Xs | e <[1 I]=!Players.I.passive:√>>
     “MTXM”:<Xs = M: grp 2 Xs | m ul = xy @WR |i|e:[P I] setTile P M,I>
     “UNIT”:<[X:@2.ul Y:@2.ul I O D:@2.ul @Xs]
               = XY:[X+WM Y+WM] = T:I,<57=Critters.TS; _=PudUnits.I>
               = T,<ø = error “Invalid unit slot: $I”
                   ;\player = !Players.O.tile:XY = !Players.O.view:(32XY-[224 224])
                   ;T =l [XY O+1 T D]@!Us>
               = r Xs>
 = get Path | <[M:@4.utf8 L:@4.ul D:@L.y @Xs]=[[M D] @Xs,r]>
 | <[[\TYPE _]@Xs]=Xs; _ = error “Invalid PUD file: $Path”> | e:[T D] {H:Handlers.T = H D}
 = l [WW-WM 0 WM WH] [0 WH-WM WW WM] [0 0 WM WH] [0 0 WW WM] | m:X xy @X | j | m:P setTile P 0
 = Players | e <[_ U] = !U.owner: U.id
   = !U.enemies: Players | m ?,1 | s ?.id≥≤U.id | filter:P
     {P.team≤≥U.team |a P.team≤≥0 |a U.team≤≥0 = P.id; P.playable |a U.playable = P.id}
   = aset U.id U Units
   = {ThisPlayer,n |a U.playable = !ThisPlayer:U.id
     ;U.nobody = U.id,delUnit
     ;√ = order \plan U}>
 = Us | e <[XY O T D] = U: unit O T
                      = {R:U.resource = !U.R: D*2500}
                      = F:U.supply-U.cost.food = {F,nz? =l [U.id \food F]@!NQs.O.add}
                      = {U.building,n = !U.dir: pick Dirs}
                      = deployUnit XY U>
 = Pal: Tilesets.TS.tiles,0.gfx'p
 = !Tints: “$Data/cfg/color.txt”,cfg,sort | m:[N@C] l N (recolor 208 Pal C)
 = ø
